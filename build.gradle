apply plugin: 'com.fkorotkov.libraries'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.fkorotkov:gradle-libraries-plugin:0.1'
  }
}

updateLibraries.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['dev', 'eap'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('dev version')
      }
    }
  }
}

subprojects {
  buildscript {
    repositories {
      jcenter()
    }
    dependencies {
      classpath libraries['org.jetbrains.kotlin:kotlin-gradle-plugin']
    }
  }

  repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { setUrl("https://repo.gradle.org/gradle/repo") }
  }

  apply plugin: 'com.fkorotkov.libraries'
  apply plugin: 'java-library'

  dependencies {
    implementation libraries['org.jetbrains.kotlin:kotlin-stdlib']
  }

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  test {
    testLogging {
      exceptionFormat = 'full'
    }
  }
}
